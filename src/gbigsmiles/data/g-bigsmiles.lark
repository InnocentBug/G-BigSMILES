big_smiles: big_smiles_molecule* big_smiles_fragment_definition*
big_smiles_molecule: smiles? _big_smiles_repeat* dot_generation?
_big_smiles_repeat: stochastic_object* smiles?

stochastic_object: "{" WS_INLINE* terminal_bond_descriptor WS_INLINE* smiles WS_INLINE* _monomer_list* WS_INLINE* end_group? WS_INLINE* terminal_bond_descriptor "}" stochastic_generation?

atom:  _bracket_atom
  | _aliphatic_organic
  | _aromatic_organic
  | "*"
  | big_smiles_fragment_declaration
  | stochastic_object

# Ok, this is not every printable_ascii character as described here: https://olsenlabmit.github.io/BigSMILES/docs/line_notation.html#simplifications-and-abbreviations, but it makes parsing so much more easy if we exclude a couple characters here.
_printable_character: /['?'-Z]/
  | /['^'-z]/
  | /[!-']/
  | /[*-;]/
  | "~"
_big_smiles_fragment_name: _printable_character+
big_smiles_fragment_declaration: "[#" _big_smiles_fragment_name "]"

big_smiles_fragment_definition: ".{#" _big_smiles_fragment_name "=" big_smiles_molecule+ "}"


_atom_or_bond_descriptor: atom | bond_descriptor

_bracket_atom: "[" _isotope? _symbol _chiral? _hcount? _charge? _class? "]"

_symbol: _element_symbols
  | _aromatic_symbols
  | "*"

bond: "-"
  | "="
  | "#"
  | "$"
  | ":"
  | "/"
  | "\\"

ringbond: bond? DIGIT
  | bond? "%" DIGIT? DIGIT

_branched_atom: _atom_or_bond_descriptor ringbond* branch*

branch: "(" bond? big_smiles_molecule* ")"

_atom_assembly: bond? _branched_atom
smiles: _branched_atom _atom_assembly*
  | smiles dot smiles


_isotope: INT

bond_descriptor_symbol: "$"
  | ">"
  | "<"

bond_descriptor_generation: "|" NUMBER "|"
  | "|" NUMBER _number_list_repeat* "|"

ladder_bond_descriptor: "[" _inner_ambi_covalent_descriptor _simple_bond_descriptor INT? "]"
  | "[" _inner_ambi_covalent_descriptor non_covalent_bond_descriptor INT? "]"

_inner_bond_descriptor: bond_descriptor_symbol INT? bond_descriptor_generation?
_inner_non_covalent_descriptor: bond_descriptor_symbol ":" INT? _non_covalent_context?
_inner_ambi_covalent_descriptor: _inner_bond_descriptor | _inner_non_covalent_descriptor

_simple_bond_descriptor: "[" _inner_bond_descriptor "]"
non_covalent_bond_descriptor: "[" _inner_non_covalent_descriptor "]"

bond_descriptor: _simple_bond_descriptor | ladder_bond_descriptor | non_covalent_bond_descriptor
terminal_bond_descriptor.-2: "[" bond_descriptor_symbol? INT? "]"

stochastic_generation: "|" stochastic_distribution "|"
stochastic_distribution: "flory_schulz(" WS_INLINE* NUMBER WS_INLINE* ")"
  | "schulz_zimm(" WS_INLINE* NUMBER WS_INLINE* "," WS_INLINE* NUMBER WS_INLINE* ")"
  | "gauss(" WS_INLINE* NUMBER WS_INLINE* "," WS_INLINE* NUMBER WS_INLINE* ")"
  | "uniform(" WS_INLINE* NUMBER WS_INLINE* "," WS_INLINE* NUMBER WS_INLINE* ")"
  | "log_normal(" WS_INLINE* NUMBER WS_INLINE* "," WS_INLINE* NUMBER WS_INLINE* ")"
  | "poisson(" WS_INLINE* NUMBER WS_INLINE* ")"

_unary_index_operator: "!"
_binary_index_operator: "~" | "&"
_index_statement: INT | _branched_index_expression | _unbranched_index_expression
_branched_index_expression: "(" WS_INLINE* _index_statement WS_INLINE* _binary_index_operator WS_INLINE* _index_statement WS_INLINE* ")"
_unbranched_index_expression:  _index_statement WS_INLINE* _binary_index_operator WS_INLINE* _index_statement
_index_expression: _unary_index_operator? WS_INLINE* _index_statement
_non_covalent_key_value_pair: WS_INLINE* "," WS_INLINE* _printable_character+ "=" _printable_character+ WS_INLINE*
_non_covalent_context: WS_INLINE* "|" WS_INLINE* _index_expression _non_covalent_key_value_pair*

_hcount: "H"
  | "H" DIGIT

_charge: "-" DIGIT?
  | "+" DIGIT?
  | "--"
  | "++"

_class: ":" INT


dot_system_size: "|" WS_INLINE* NUMBER WS_INLINE* "|"
dot_generation: dot dot_system_size
dot: "."

_number_list_repeat.-1: " " NUMBER

_monomer_list: WS_INLINE* "," WS_INLINE* smiles
end_group.-1: ";" WS_INLINE* smiles _monomer_list*


%import common.INT
%import common.SIGNED_NUMBER
%import common.NUMBER
%import common.DIGIT
%import common.WS
%import common.WS_INLINE

_aliphatic_organic:  "B"
  | "C"
  | "N"
  | "O"
  | "S"
  | "P"
  | "F"
  | "Cl"
  | "Br"
  | "I"

_aromatic_organic: "b"
  | "c"
  | "n"
  | "o"
  | "s"
  | "p"

_aromatic_symbols: "b"
  | "c"
  | "n"
  | "o"
  | "p"
  | "s"
  | "se"
  | "as"

_chiral: "@"
  | "@@"
  | "@TH1"
  | "@TH2"
  | "@AL1"
  | "@AL2"
  | "@SP1"
  | "@SP2"
  | "@SP3"
  | "@TB" DIGIT? DIGIT
  | "@OH" DIGIT? DIGIT

_element_symbols: "H"
  | "He"
  | "Li"
  | "Be"
  | "B"
  | "C"
  | "N"
  | "O"
  | "F"
  | "Ne"
  | "Na"
  | "Mg"
  | "Al"
  | "Si"
  | "P"
  | "S"
  | "Cl"
  | "Ar"
  | "K"
  | "Ca"
  | "Sc"
  | "Ti"
  | "V"
  | "Cr"
  | "Mn"
  | "Fe"
  | "Co"
  | "Ni"
  | "Cu"
  | "Zn"
  | "Ga"
  | "Ge"
  | "As"
  | "Se"
  | "Br"
  | "Kr"
  | "Rb"
  | "Sr"
  | "Y"
  | "Zr"
  | "Nb"
  | "Mo"
  | "Tc"
  | "Ru"
  | "Rh"
  | "Pd"
  | "Ag"
  | "Cd"
  | "In"
  | "Sn"
  | "Sb"
  | "Te"
  | "I"
  | "Xe"
  | "Cs"
  | "Ba"
  | "Hf"
  | "Ta"
  | "W"
  | "Re"
  | "Os"
  | "Ir"
  | "Pt"
  | "Au"
  | "Hg"
  | "Tl"
  | "Pb"
  | "Bi"
  | "Po"
  | "At"
  | "Rn"
  | "Fr"
  | "Ra"
  | "Rf"
  | "Db"
  | "Sg"
  | "Bh"
  | "Hs"
  | "Mt"
  | "Ds"
  | "Rg"
  | "Cn"
  | "Fl"
  | "Lv"
  | "La"
  | "Ce"
  | "Pr"
  | "Nd"
  | "Pm"
  | "Sm"
  | "Eu"
  | "Gd"
  | "Tb"
  | "Dy"
  | "Ho"
  | "Er"
  | "Tm"
  | "Yb"
  | "Lu"
  | "Ac"
  | "Th"
  | "Pa"
  | "U"
  | "Np"
  | "Pu"
  | "Am"
  | "Cm"
  | "Bk"
  | "Cf"
  | "Es"
  | "Fm"
  | "Md"
  | "No"
  | "Lr"
